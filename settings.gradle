include ':app'
include ':detekt-gitlab-output-plugin'
include ':openvpn'
include ':shared-test-code'

// Use core libs from maven artifacts or from git submodule using Gradle's included build:
// - to enable/disable locally: gradle.properties > useCoreGitSubmodule
// - to enable/disable on CI: .gitlab-ci.yml > ORG_GRADLE_PROJECT_useCoreGitSubmodule
def coreSubmoduleDir = new File(rootDir, "proton-libs")
ext.setProperty("coreSubmoduleDir", coreSubmoduleDir)
def includeCoreLibsHelper = new File(coreSubmoduleDir, "gradle/include-core-libs.gradle.kts")
if (includeCoreLibsHelper.exists()) {
    apply(from: includeCoreLibsHelper)
} else if (extensions.extraProperties["useCoreGitSubmodule"].toBoolean()) {
    includeBuild("proton-libs")
    println("Core libs from git submodule `$coreSubmoduleDir`")
}

buildCache {
    local {
        if (System.getenv("CI") == "true") {
            directory = file("build/gradle-build-cache")
        }
        removeUnusedEntriesAfterDays = 3
    }
}
