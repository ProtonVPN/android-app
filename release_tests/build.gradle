import groovy.json.StringEscapeUtils

/*
 * Copyright (c) 2023. Proton AG
 *
 * This file is part of ProtonVPN.
 *
 * ProtonVPN is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * ProtonVPN is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with ProtonVPN.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
}

// TODO: share these with project :app?
def storeKeyAlias = project.hasProperty('keyStoreKeyAlias') ? keyStoreKeyAlias : 'proton'
def storePass = project.hasProperty('keyStorePassword') ? keyStorePassword : "\"Store password\""
def storeKeyPass = project.hasProperty('keyStoreKeyPassword') ? keyStoreKeyPassword : "\"Store key password\""
def testAccountPassword = project.hasProperty('testAccountPassword') ? testAccountPassword : "\"Pass\""

android {
    namespace "com.protonvpn.android.release_tests"
    compileSdk 34

    signingConfigs {
        release {
            storeFile file(rootProject.ext.keyStoreFilePath)
            storePassword storePass
            keyAlias storeKeyAlias
            keyPassword storeKeyPass
        }
    }

    defaultConfig {
        applicationId "com.protonvpn.android.release_tests"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdk 34
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "TEST_ACCOUNT_PASSWORD",  testAccountPassword
        def lokiEndpoint = System.getenv("LOKI_ENDPOINT")
        def commitSha = System.getenv("CI_COMMIT_SHORT_SHA")
        buildConfigField "String", "LOKI_ENDPOINT",  "\"${StringEscapeUtils.escapeJava(lokiEndpoint)}\""
        buildConfigField "String", "CI_COMMIT_SHORT_SHA",  "\"${StringEscapeUtils.escapeJava(commitSha)}\""
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
}

dependencies {

    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version"
    implementation "androidx.test.ext:junit:1.1.5"
    implementation "androidx.test:runner:1.5.2"
    implementation "androidx.test.uiautomator:uiautomator:2.3.0"
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation "me.proton.test:fusion:0.9.62"
}
