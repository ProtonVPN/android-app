/*
 * Copyright (c) 2017 Proton Technologies AG
 *
 * This file is part of ProtonVPN.
 *
 * ProtonVPN is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * ProtonVPN is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with ProtonVPN.  If not, see <https://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.android.application'
    id 'com.github.triplet.play' version '2.7.2'
}

apply plugin: 'io.gitlab.arturbosch.detekt'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco'
apply plugin: 'com.jaredsburrows.license'

detekt {
    failFast = false
    config = files("$projectDir/config/detekt/detekt.yml")
    reports {
        xml {
            enabled = false
        }
        html {
            enabled = false
        }
        txt {
            enabled = false
        }
        custom {
            reportId = "DetektQualityOutputReport"
            destination = file("build/reports/detekt.json")
        }
    }
}

jacoco {
    toolVersion = '0.8.4'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}


task jacocoTestReport(type: JacocoReport, dependsOn: ['testProdDebugUnitTest']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', 'ch.protonmail.android.utils.nativelib']
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/prodDebug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.setFrom files([mainSrc])
    classDirectories.setFrom files([debugTree])
    executionData.setFrom fileTree(dir: "$buildDir", includes: [
            "**/*.exec",
            "**/*.ec"
    ])
    afterEvaluate {
        classDirectories.setFrom files(classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/ch/protonmail/**')
        })
    }
}
task coverageReport() {
    dependsOn jacocoTestReport

    def reportFile = project.file("build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml")
    inputs.file(reportFile)

    doLast {
        def slurper = new XmlSlurper()
        slurper.setFeature("http://apache.org/xml/features/disallow-doctype-decl", false)
        slurper.setFeature("http://apache.org/xml/features/nonvalidating/load-external-dtd", false)
        def xml = slurper.parse(reportFile)
        def counter = xml.counter.find {
            node -> node.@type == 'INSTRUCTION'
        }
        def missed = counter.@missed.toDouble()
        def covered = counter.@covered.toDouble()
        def total = missed + covered
        def percentage = covered / total * 100

        printf "Missed %.0f branches%n", missed
        printf "Covered %.0f branches%n", covered
        printf "Total %.0f%%%n", percentage
    }
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task updateLicensesJson(dependsOn: 'licenseProdReleaseReport') {
    doLast {
        File json = new File("$project.projectDir/build/reports/licenses/licenseProdReleaseReport.json")
        File out = new File("$project.projectDir/src/main/assets/open_source_licenses.js")
        out.text = 'dependencies = ' + json.text
    }
}

licenseReport {
    generateHtmlReport = false
    generateJsonReport = true
    copyHtmlReportToAssets = false
    copyJsonReportToAssets = false
}

def productionServer = "\"api.protonvpn.ch\""
def testingServer = project.hasProperty('testingServerUrl') ? testingServerUrl : productionServer
def storeFilePath = project.hasProperty('keyStoreFilePath') ? keyStoreFilePath : "protonkey.jks"
def storeKeyAlias = project.hasProperty('keyStoreKeyAlias') ? keyStoreKeyAlias : 'proton'
def storePass = project.hasProperty('keyStorePassword') ? keyStorePassword : "\"Store password\""
def storeKeyPass = project.hasProperty('keyStoreKeyPassword') ? keyStoreKeyPassword : "\"Store key password\""
def sentryDns = project.hasProperty('sentryDns') ? sentryDns : "null"
def preferencesSalt = project.hasProperty('preferencesSalt') ? preferencesSalt : "\"Salt\""
def preferencesKey = project.hasProperty('preferencesKey') ? preferencesKey : "\"Key\""
def serviceAccountCredentialsPath = project.hasProperty('serviceAccountFilePath') ? serviceAccountFilePath : "service_account.json"
def appId = project.hasProperty('appId') ? appId : "ch.protonvpn.android"

android {
    def helpers = new Helpers()

    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    useLibrary 'org.apache.http.legacy'
    flavorDimensions "default"
    signingConfigs {
        release {
            storeFile file(storeFilePath)
            storePassword storePass
            keyAlias storeKeyAlias
            keyPassword storeKeyPass
        }
    }
    viewBinding {
        enabled = true
    }
    dexOptions {
        javaMaxHeapSize "4g"
        incremental true
        preDexLibraries true
    }
    defaultConfig {
        applicationId appId
        minSdkVersion 21
        targetSdkVersion 28
        multiDexEnabled true
        versionName helpers.fullVersionName
        versionCode helpers.getVersionCode(0)
        archivesBaseName = helpers.archivesBaseName
        testInstrumentationRunner "com.protonvpn.TestsRunner"
        vectorDrawables.useSupportLibrary = true
        javaCompileOptions.annotationProcessorOptions {
            arguments['dagger.android.experimentalUseStringKeys'] = 'true'
        }
        buildConfigField "String", "Sentry_DSN",  sentryDns
        buildConfigField "String", "PREF_SALT",  preferencesSalt
        buildConfigField "String", "PREF_KEY",  preferencesKey
    }
    dataBinding {
        enabled = true
    }
    productFlavors {
        dev {
            versionNameSuffix "-dev"
            applicationIdSuffix '.dev'
            resConfigs "en", "xxhdpi"
            buildConfigField "String", "API_DOMAIN", productionServer
        }

        blue {
            versionNameSuffix "-blue"
            buildConfigField "String", "API_DOMAIN", testingServer
        }

        prod {
            buildConfigField "String", "API_DOMAIN", productionServer
        }
    }
    buildTypes {
        debug {
            testCoverageEnabled true
            ext.alwaysUpdateBuildId = false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        animationsDisabled true
        unitTests {
            includeAndroidResources = true
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/AL2.0'
    }

    sourceSets {
        main.res.srcDirs += 'src/main/res_flags'
        main {
           assets.srcDirs = ['src/main/assets', 'assets', 'ovpnlibs/assets', '../openvpn/build/ovpnassets']
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    splits {
        abi {
            enable project.hasProperty('abiSplit')
            reset()
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            universalApk project.hasProperty('buildUniversalApk')
        }
    }
}

play {
    serviceAccountCredentials = file(serviceAccountCredentialsPath)
    track = 'internal'
    releaseName = Helpers.fullVersionName
}

task publishGenerateReleaseNotes {
    doLast {
        File releaseNotesDir = new File("$project.projectDir/src/main/play/release-notes/en-US")
        releaseNotesDir.mkdirs()
        File releaseNotesFile = new File(releaseNotesDir, "default.txt")
        releaseNotesFile.newWriter().withWriter { w ->
            // Limit of 500 chars on Google Play console for release notes
            w << Helpers.getReleaseNotes(490)
        }
    }
}

task publishInternal {
    if (name in project.gradle.startParameter.taskNames) {
        def lastSha = System.getenv("CI_COMMIT_BEFORE_SHA")
        if (lastSha == null || lastSha.matches("0*") ||
                Helpers.exec("git rev-list --count ${lastSha}..HEAD").trim().toInteger() == 0) {
            println "skipping publishing..."
        } else {
            dependsOn 'publishGenerateReleaseNotes'
            dependsOn 'publishProdReleaseApk'
            doLast {
                Helpers.notifyPublishOnSlack()
            }
        }
    }
}

task prepareGuestHoleServers {
    doLast {
        def guestHoleUrl = System.getenv("GUEST_HOLE_URL")
        if (guestHoleUrl == null) {
            copy {
                from "$project.projectDir/config/GuestHoleServers.json"
                into "$project.projectDir/src/main/assets"
            }
        } else {
            def allServers = new JsonSlurper().parse(new URL(guestHoleUrl))["LogicalServers"]
            def candidateServers = allServers.stream().filter { s ->
                def country = s["ExitCountry"]
                def secureCore = (s["Features"] & 1) == 1
                !secureCore && s["Status"] == 1 && !country.equalsIgnoreCase("se") && !country.equalsIgnoreCase("is")
            }.collect()
            Collections.shuffle(candidateServers)
            def servers = candidateServers.take(10)

            File dir = new File("$project.projectDir/src/main/assets")
            File serversFile = new File(dir, "GuestHoleServers.json")
            serversFile.newWriter().withWriter { w ->
                w << JsonOutput.prettyPrint(new JsonOutput().toJson(servers))
            }
        }
    }
}

preBuild.dependsOn prepareGuestHoleServers

ext.abiCodes = ['armeabi-v7a':1, 'arm64-v8a':2, x86:3, x86_64:4]

import com.android.build.OutputFile
import groovy.json.JsonOutput
import groovy.json.JsonSlurper

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def abiCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
        if (abiCode != null) {
            output.versionCodeOverride = Helpers.getVersionCode(abiCode)
        }
    }
}

class Helpers {

    // Versioning:
    //  Version name: M.m.D.R
    //  M.m (major.minor) come from the last tag on development branch of this form (e.g. "2.1")
    //  D - number of commits on development since M.m
    //  R - number of commits on release branch (from development)
    //  Version code: AMMmmDDRR as decimal integer
    //  A - abiCode + 1
    static int getVersionCode(def abiCode) {
        def name = fullVersionName
        def versions = name.split("\\.")
        def major = versions[0].toInteger()
        def minor = versions[1].toInteger()
        def dev = versions[2].toInteger()
        def release = versions[3].toInteger()

        // Max version code allowed by android is 2_000_000_000
        assert major < 100 && minor < 100 && dev < 100 && release < 100

        // abiCode + 1 needed to always produce higher version code than in previous versioning
        // scheme
        // 0AMMMmmPP - old
        // AMMmmDDRR - new
        def code = (abiCode + 1) * 100_000_000 +
                major * 1_000_000 +
                minor*10_000 +
                dev * 100 +
                release

        // Just some sanity check because there's no turning back once we accidentally publish
        // some large version code
        assert code < 600_000_000 && major < 50

        return code
    }

    static String getFullVersionName() {
        // Find last tag in the form M.m.D.R, D and R are optional
        def tag = exec('git tag --merged HEAD').trim().split('\n').reverse().find { it.matches('\\d+(\\.\\d+){1,3}') }
        def tagSplit = Arrays.stream(tag.split("\\.")).mapToInt { it.toInteger() }.toArray()
        def major = tagSplit[0]
        def minor = tagSplit[1]
        def dev = tagSplit.size() > 2 ? tagSplit[2] : 0
        def release = tagSplit.size() > 3 ? tagSplit[3] : 0

        def onMaster = System.getenv("CI_COMMIT_BRANCH") == "master" ||
                exec("git rev-parse --abbrev-ref HEAD").trim() == "master"
        // On master or public repo just use last tag for version name
        if (!onMaster && exec("git rev-parse --verify origin/development", false) != null) {
            def branchPoint = exec("git merge-base origin/development HEAD").trim()
            // add #commits from tag to branch point with development to dev
            dev += exec("git rev-list --count ${tag}..${branchPoint}").trim().toInteger()
            // add #commits from branch point to HEAD to release
            release += exec("git rev-list --count origin/development..HEAD").trim().toInteger()
        }
        return "${major}.${minor}.${dev}.${release}"
    }

    static String getReleaseNotes(int trimAt) {
        def lastSha = System.getenv("CI_COMMIT_BEFORE_SHA")
        def notes = exec(["git", "log", "${lastSha}..HEAD", "--pretty=format:- %s"])
        if (notes.length() > trimAt)
            return notes.take(trimAt) + "\n..."
        return notes
    }

    static void notifyPublishOnSlack() {
        def hook = System.getenv("SLACK_PUBLISH_HOOK")
        if (hook == null)
            return
        def json = new JsonOutput().toJson(["text": ":android: :protonvpn: ${getFullVersionName()} released to internal\n" +
                "Release notes:\n" +
                "${getReleaseNotes(1000)}"])
        exec(["curl", "-X", "POST", "-H", "'Content-type: application/json'", "--data", json, hook])
    }

    static String getArchivesBaseName() {
        return "ProtonVPN-" + fullVersionName + "(" + getVersionCode(0) + ")"
    }

    static String exec(String cmd, boolean throwOnError = true) {
        def out = new StringBuffer()
        def err = new StringBuffer()
        def proc = cmd.execute()
        proc.waitForProcessOutput(out, err)
        if (proc.exitValue() != 0) {
            if (throwOnError)
                throw new GradleScriptException("Error executing: ${cmd}", new RuntimeException(err.toString()))
            else
                return null
        }
        return out.toString()
    }

    static String exec(ArrayList<String> cmd, boolean throwOnError = true) {
        def out = new StringBuffer()
        def err = new StringBuffer()
        def proc = cmd.execute()
        proc.waitForProcessOutput(out, err)
        if (proc.exitValue() != 0) {
            if (throwOnError)
                throw new GradleScriptException("Error executing: ${cmd}", new RuntimeException(err.toString()))
            else
                return null
        }
        return out.toString()
    }
}

kapt {
    arguments {
        arg('dagger.android.experimentalUseStringKeys')
    }
}

tasks.register("getArchivesName"){
    doLast {
        println archivesBaseName
    }
}

task checkstyle(type: Checkstyle) {
    configFile = file("${rootDir}/app/config/checkstyle/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
    ignoreFailures = false
}

ext.jacksonVersion = '2.11.0'
ext.daggerVersion = '2.26'

repositories {
    maven { url "https://clojars.org/repo/" }
    google()
    mavenCentral()
}
configurations {
    detekt
}

apply plugin: 'checkstyle'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    debugImplementation 'androidx.test.espresso.idling:idling-concurrent:3.2.0'
    debugImplementation('com.jakewharton.espresso:okhttp3-idling-resource:1.0.0') {
        exclude module: 'support-annotations'
    }
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.3'

    detekt 'io.gitlab.arturbosch.detekt:detekt-formatting:1.9.1'
    detekt 'io.gitlab.arturbosch.detekt:detekt-cli:1.9.1'
    detektPlugins 'pm.algirdas.detekt:codeanalysis:0.3.2'
    detektPlugins 'io.gitlab.arturbosch.detekt:detekt-formatting:1.9.1'

    implementation 'com.proton:srp:1.0.0@aar'
    implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
    implementation 'androidx.test.espresso:espresso-idling-resource:3.2.0'
    // 3.10 of commons-lang3 causes NoClassDefFoundError on start (to be investigated)
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'commons-codec:commons-codec:1.14'
    implementation 'com.qozix:tileview:2.2.7'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    implementation 'com.jakewharton:butterknife:10.2.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.github.lzyzsd:circleprogress:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.core:core:1.3.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'com.daimajia.easing:library:2.1@aar'
    implementation 'com.squareup:otto:1.3.8'
    implementation 'io.sentry:sentry-android:1.7.30'
    implementation 'com.makeramen:roundedimageview:2.3.0'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.github.shts:TriangleLabelView:1.1.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.github.VictorAlbertos:RxActivityResult:0.5.0-2.x'
    implementation 'com.jakewharton.rxbinding2:rxbinding-design:2.2.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'com.scottyab:aes-crypto:0.0.5'
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.11.0'
    implementation 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:2.3.0'
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'net.danlew:android.joda:2.10.6'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.github.anrwatchdog:anrwatchdog:1.4.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.7.2'
    implementation 'com.getkeepsafe.relinker:relinker:1.3.1'
    implementation 'me.relex:circleindicator:1.2.2@aar'
    implementation 'com.github.douglasjunior:android-simple-tooltip:0.2.3'
    implementation 'ru.egslava:MaskedEditText:1.0.5'
    implementation 'com.evernote:android-state:1.4.1'
    implementation 'androidx.work:work-runtime:2.3.4'
    implementation 'com.github.akarnokd:rxjava2-extensions:0.20.10'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.7'
    implementation "com.datatheorem.android.trustkit:trustkit:1.1.2"
    implementation 'com.xwray:groupie:2.7.2'
    implementation 'com.xwray:groupie-databinding:2.7.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'org.minidns:minidns-hla:0.3.4'
    implementation 'com.github.tony19:logback-android:2.0.0'
    implementation 'org.slf4j:slf4j-api:1.7.30'

    // https://github.com/grpc/grpc-java/issues/3633 https://github.com/google/dagger/issues/1449
    implementation 'com.github.pengrad:jdk9-deps:1.0'

    // Dagger dependencies
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    kapt 'com.jakewharton:butterknife-compiler:10.2.1'
    kapt 'com.evernote:android-state-processor:1.4.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Test dependencies
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$daggerVersion"
    kaptAndroidTest "com.google.dagger:dagger-android-processor:$daggerVersion"

    testImplementation 'joda-time:joda-time:2.10.6'
    testImplementation 'junit:junit:4.13'
    testImplementation 'io.mockk:mockk:1.10.0'
    androidTestImplementation 'io.mockk:mockk-android:1.10.0'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.7'

    androidTestImplementation('androidx.test.espresso:espresso-idling-resource:3.2.0') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation 'com.azimolabs.conditionwatcher:conditionwatcher:0.2'

    // Core library
    androidTestImplementation 'androidx.test:core:1.2.0'
    testImplementation "androidx.arch.core:core-testing:2.0.1"
    androidTestImplementation "androidx.arch.core:core-testing:2.0.1"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.7'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.ext:truth:1.2.0'
    androidTestImplementation 'com.google.truth:truth:1.0.1'

    // Espresso dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.2.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    implementation project(path: ':openvpn')
}
